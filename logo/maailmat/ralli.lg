maailma "grid

;; the world

;world.initialise "ralli/esim.wld

;; finding hit tiles from a world

to distance2 :pos1 :pos2
   localmake "xd (first :pos1) - (first :pos2)
   localmake "yd (last :pos1) - (last :pos2)
   op :xd * :xd + :yd * :yd
end

to grid.closestto :pos :coll
  if emptyp :coll [op []]
  if emptyp bf :coll [op first :coll]
  localmake "res grid.closestto :pos bf :coll
  if (distance2 :pos first :res) < (distance2 :pos first first :coll) [op :res]
  op first :coll
end

to grid.firsthit :grid :pos1 :pos2
  op grid.closestto :pos1 grid.transitions :grid :pos1 :pos2
end

;; moving with checked collisions

Make ".lastpos []

to checkcollisions
  if not emptyp :.lastpos [makecollisions :world :.lastpos pos]
  make ".lastpos pos
end

to makecollisions :grid :src :dst
  localmake "firsthit grid.firsthit :grid :src :dst
  if emptyp :firsthit [stop]
  foreach last :firsthit [if not emptyp first ? 
    [apply word first ? ".collide fput first :firsthit bf ?]]
end

to move.protected :.move
  checkcollisions
  run :.move
  checkcollisions
end

to protect.command :cmd
  make "redefp "true
  localmake "origcmd word :cmd ".orig
  copydef :origcmd :cmd
  define :cmd `[[arg] [move.protected [apply ,[word "" :origcmd] (list :arg)]]]
  bury :cmd
  make "redefp "false
end

foreach [fd forward et eteen bk back ta taakse setpos paikkaan setx sety] ~
   "protect.command
