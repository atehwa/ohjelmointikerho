maailma "3dops
maailma "ohjaus

miten 3dlento
  3dhome
  3dlentosilmu alkukivet []
valmis

miten 3dlentosilmu :kivet :tie
  ohjaa
  pyyhi
  3dpiirrä alkuun (lista [] 7 alus 3dpos) :kivet
  törmää :kivet
  odota 3
  3dlentosilmu päivitäkivet :kivet :tie
valmis

miten 3dpiirrä :oliot
  jos tyhjäkö :oliot [seis]
  nimeä "olio eka :oliot
  väri eka loput :olio
  äläpiirrä
  foreach vika :olio "3dsetpos
  3dsetpos eka vika :olio
  3dpiirrä loput :oliot
valmis

miten alkukivet
  anna map [satunnaiskivi ? * 1000] luettele 2 12
valmis

miten alus :paikka
  anna map [3dadd :paikka ?] [[-20 -20 -20] [20 -20 -20] [20 20 -20] [20 20 20] [-20 20 20] [-20 -20 20]]
valmis

miten kivi :keskus
  nimeä "pisteet []
  toista 10 [
    nimeä "x 100 - satunnainen 200
    nimeä "yz sqrt 10000 - :x*:x
    nimeä "y :yz * cos kerta * 36
    nimeä "z :yz * sin kerta * 36
    push "pisteet 3dadd :keskus 3dpoint :x :y :z
  ]
  anna :pisteet
valmis

miten ohjaa
  luenäppis
  jos painetaanko "a [3dturnyaw -4]
  jos painetaanko "d [3dturnyaw 4]
  jos painetaanko "s [3dturnpitch -4]
  jos painetaanko "w [3dturnpitch 4]
  3dmove 3dunrotate [0 0 110]
valmis

miten päivitäkivet :kivet
  nimeä "jäljellä filter [-500 < 3dz 3dmakelocal eka ?] :kivet
  jos 10 < pituus :jäljellä [anna :jäljellä]
  anna alkuun satunnaiskivi 2000 + sat 2500 :jäljellä
valmis

miten satunnaiskivi :etäisyys
  nimeä "kohta 3dadd 3dpos 3dunrotate 3dpoint 300 - sat 600 300 - sat 600 :etäisyys
  anna (list :kohta (1 + sat 16) kivi :kohta)
valmis

miten törmää :kivet
  jos tyhjäkö :kivet [seis]
  nimeä "kivenpaikka eka eka :kivet
  nimeä "omapaikka 3dpos
  nimeä "xd (3dx :kivenpaikka) - (3dx :omapaikka)
  nimeä "yd (3dy :kivenpaikka) - (3dy :omapaikka)
  nimeä "zd (3dz :kivenpaikka) - (3dz :omapaikka)
  jos :xd * :xd + :yd * :yd + :zd * :zd < 10000 [
    sano [haha, osuit]
    lopeta
  ]
  törmää loput :kivet
valmis
